<!DOCTYPE html>
<html>
<head>
<title>ebuild для Gentoo</title>
</head>
<body>
<h1>ebuild для Gentoo</h1>

<h2>(gentoo)monodevelop-3.0.2-r1.ebuild</h2>
<pre><xmp>
# Copyright 1999-2013 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-x86/dev-util/monodevelop/monodevelop-3.0.2-r1.ebuild,v 1.5 2013/10/12 12:09:05 pacho Exp $

EAPI=4
inherit fdo-mime gnome2-utils mono versionator eutils

DESCRIPTION="Integrated Development Environment for .NET"
HOMEPAGE="http://www.monodevelop.com/"
SRC_URI="http://download.mono-project.com/sources/${PN}/${P}.tar.bz2"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="amd64 x86"
IUSE="+subversion +git"

RDEPEND=">=dev-lang/mono-2.10.9
	>=dev-dotnet/gconf-sharp-2.24.0
	>=dev-dotnet/glade-sharp-2.12.9
	>=dev-dotnet/gnome-sharp-2.24.0
	>=dev-dotnet/gnomevfs-sharp-2.24.0
	>=dev-dotnet/gtk-sharp-2.12.9
	>=dev-dotnet/mono-addins-0.6[gtk]
	>=dev-dotnet/xsp-2
	dev-util/ctags
	sys-apps/dbus[X]
	|| (
		www-client/firefox
		www-client/firefox-bin
		www-client/seamonkey
		)
	subversion? ( dev-vcs/subversion )
	!<dev-util/monodevelop-boo-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-java-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-database-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-debugger-gdb-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-debugger-mdb-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-vala-$(get_version_component_range 1-2)"
DEPEND="${RDEPEND}
	dev-util/intltool
	virtual/pkgconfig
	sys-devel/gettext
	x11-misc/shared-mime-info"

MAKEOPTS="${MAKEOPTS} -j1"

src_prepare() {
	epatch "${FILESDIR}"/${P}-pc-fix.patch
	epatch "${FILESDIR}"/${P}-nowarningerrors.patch
}

src_configure() {
	econf \
		--disable-update-mimedb \
		--disable-update-desktopdb \
		--enable-monoextensions \
		--enable-gnomeplatform \
		$(use_enable subversion) \
		$(use_enable git)
}

pkg_preinst() {
	gnome2_icon_savelist
}

pkg_postinst() {
	gnome2_icon_cache_update
	fdo-mime_mime_database_update
	fdo-mime_desktop_database_update
}

pkg_postrm() {
	gnome2_icon_cache_update
	fdo-mime_mime_database_update
	fdo-mime_desktop_database_update
}
</xmp></pre>

<span style="color:red">Для чего символы <b>+</b> в строке IUSE ?</span>
<br />
<span style="color:red">Почему monodevelop ставится не в слоты (это было бы удобно)?</span>

<h2>(dotnet)monodevelop-5.0.1.ebuild</h2>
<pre><xmp>
# Copyright 1999-2014 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI="5"
inherit fdo-mime gnome2-utils dotnet versionator eutils

DESCRIPTION="Integrated Development Environment for .NET"
HOMEPAGE="http://www.monodevelop.com/"
SRC_URI="http://download.mono-project.com/sources/${PN}/${P}-0.tar.bz2"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="+subversion +git doc"

RDEPEND=">=dev-lang/mono-3.2.8
	>=dev-dotnet/gnome-sharp-2.24.2-r1
	>=dev-dotnet/gtk-sharp-2.12.21
	>=dev-dotnet/mono-addins-1.0[gtk]
	doc? ( dev-util/mono-docbrowser )
	>=dev-dotnet/xsp-2
	dev-util/ctags
	sys-apps/dbus[X]
	subversion? ( dev-vcs/subversion )
	!<dev-util/monodevelop-boo-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-java-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-database-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-debugger-gdb-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-debugger-mdb-$(get_version_component_range 1-2)
	!<dev-util/monodevelop-vala-$(get_version_component_range 1-2)"
DEPEND="${RDEPEND}
	dev-util/intltool
	virtual/pkgconfig
	sys-devel/gettext
	x11-misc/shared-mime-info
	x11-terms/xterm"
MAKEOPTS="${MAKEOPTS} -j1" #nowarn

src_configure() {
	econf \
		--disable-update-mimedb \
		--disable-update-desktopdb \
		--enable-monoextensions \
		--enable-gnomeplatform \
		$(use_enable subversion) \
		$(use_enable git)
}

pkg_preinst() {
	gnome2_icon_savelist
}

pkg_postinst() {
	gnome2_icon_cache_update
	fdo-mime_mime_database_update
	fdo-mime_desktop_database_update
}

pkg_postrm() {
	gnome2_icon_cache_update
	fdo-mime_mime_database_update
	fdo-mime_desktop_database_update
}
</xmp></pre>

<table border="1" cellspacing="0" cellpadding="3">
<tr><th>3.0.2</th><th>5.0.1</th></tr>

<tr><td>dev-lang/mono-2.10.9</td><td>dev-lang/mono-3.2.8</td></tr>
<tr><td>&gt;=dev-dotnet/gnome-sharp-2.24.0</td><td>&gt;=dev-dotnet/gnome-sharp-2.24.2-r1</td></tr>
<tr><td>&gt;=dev-dotnet/gtk-sharp-2.12.9</td><td>&gt;=dev-dotnet/gtk-sharp-2.12.21</td></tr>
<tr><td>&gt;=dev-dotnet/mono-addins-0.6[gtk]</td><td>&gt;=dev-dotnet/mono-addins-1.0[gtk]</td></tr>
<tr><td>&gt;=dev-dotnet/xsp-2</td><td>&gt;=dev-dotnet/xsp-2</td></tr>
<tr><td>dev-util/ctags</td><td>dev-util/ctags</td></tr>
<tr><td>sys-apps/dbus</td><td>sys-apps/dbus</td></tr>
<tr><td>&gt;=dev-dotnet/gconf-sharp-2.24.0</td><td>&nbsp;</td>
<tr><td>&gt;=dev-dotnet/glade-sharp-2.12.9</td><td>&nbsp;</td>
<tr><td>&gt;=dev-dotnet/gnomevfs-sharp-2.24.0</td><td>&nbsp;</td>
<tr><td>&nbsp;</td><td>doc? ( dev-util/mono-docbrowser )</td></tr>
</table>

<span style="color:red">Что это за пакеты, которые стали ненужны? Для чего они были, куда делись?</span>

</body>
</html>
